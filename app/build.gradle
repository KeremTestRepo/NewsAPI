apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs.kotlin'

def versionMajor = 0
def versionMinor = 0
def versionPatch = 1
def versionBuild = 0
def versionStage = "alpha"

android {
    compileSdkVersion versions.compileSdk
    defaultConfig {
        applicationId 'com.necatisozer.newsapi'
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        if (!versionStage.isEmpty()) {
            versionName "${versionName}-${versionStage}"
        }
        archivesBaseName = "$applicationId-v$versionName($versionCode)"
        testInstrumentationRunner deps.androidX.test.instrumentationRunner
        vectorDrawables.useSupportLibrary = true
        buildConfigField "String", "GIT_SHA", "\"${gitSha()}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime()}\""
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.debug'
        }
    }

    dataBinding { enabled true }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
}

dependencies {
    implementation deps.kotlin.stdlib
    implementation deps.androidX.appCompat
    implementation deps.androidX.core.ktx
    implementation deps.androidX.constraintLayout
    testImplementation deps.androidX.test.jUnit.ktx
    androidTestImplementation deps.androidX.test.runner
    androidTestImplementation deps.androidX.test.espresso.core
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs.kotlin'

def versionMajor = 0
def versionMinor = 0
def versionPatch = 1
def versionBuild = 0
def versionStage = "alpha"

android {
    compileSdkVersion versions.compileSdk
    defaultConfig {
        applicationId 'com.necatisozer.newsapi'
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        if (!versionStage.isEmpty()) {
            versionName "${versionName}-${versionStage}"
        }
        archivesBaseName = "$applicationId-v$versionName($versionCode)"
        testInstrumentationRunner deps.androidX.test.instrumentationRunner
        vectorDrawables.useSupportLibrary = true
        buildConfigField "String", "GIT_SHA", "\"${gitSha()}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime()}\""
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.debug'
        }
    }

    dataBinding { enabled true }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
    kotlinOptions { jvmTarget = '1.8' }
}

dependencies {
    implementation project(':commonandroid')
    implementation project(':data')
    implementation project(':domain')

    implementation deps.androidX.activity.ktx
    implementation deps.androidX.appCompat
    implementation deps.androidX.cardView
    implementation deps.androidX.collection.ktx
    implementation deps.androidX.constraintLayout
    implementation deps.androidX.core.ktx
    implementation deps.androidX.fragment.ktx
    implementation deps.androidX.lifecycle.extensions
    implementation deps.androidX.lifecycle.viewModelKtx
    implementation deps.androidX.navigation.fragment
    implementation deps.androidX.navigation.fragmentKtx
    implementation deps.androidX.navigation.ui
    implementation deps.androidX.navigation.uiKtx
    implementation deps.androidX.recyclerView
    implementation deps.glide.glide
    implementation deps.google.material
    implementation deps.kotlin.coroutines.android
    implementation deps.splitties.activities
    implementation deps.splitties.alertDialog
    implementation deps.splitties.bundle
    implementation deps.splitties.fragments
    implementation deps.splitties.fragmentArgs
    implementation deps.splitties.materialColors
    implementation deps.splitties.snackbar
    implementation deps.splitties.toast
    implementation deps.splitties.arch.lifecycle
    implementation deps.splitties.views.views
    implementation deps.splitties.views.appCompat

    kapt deps.androidX.lifecycle.compiler
    kapt deps.glide.compiler
    kapt deps.google.dagger.compiler

    debugImplementation deps.stetho.stetho

    testImplementation deps.androidX.test.jUnit.ktx
    androidTestImplementation deps.androidX.test.core.ktx
    androidTestImplementation deps.androidX.test.jUnit.ktx
    androidTestImplementation deps.androidX.test.runner
    androidTestImplementation deps.androidX.test.espresso.core
}
